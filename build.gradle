buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

plugins {
    id 'net.nemerosa.versioning' version '1.5.0'
}

apply plugin: 'io.codearte.nexus-staging'

/**
 * Meta information
 */

allprojects {
    group = 'net.nemerosa'
    version = versioning.info.display
}

/**
 * Dependencies
 */

ext {
    springVersion = '4.1.5.RELEASE'
    libraries = [
            commons_lang    : 'org.apache.commons:commons-lang3:3.3.2',
            jackson_databind: 'com.fasterxml.jackson.core:jackson-databind:2.3.3',
            lombok          : 'org.projectlombok:lombok:1.16.4',
            spring_webmvc   : "org.springframework:spring-webmvc:${springVersion}",
            junit           : 'junit:junit:4.11',
    ]
}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'

}

/**
 * Publication
 */

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        // Signature of artifacts

        apply plugin: 'signing'

        signing {
            sign jar
            sign javadocJar
            sign sourcesJar
        }

        // OSSRH publication

        apply plugin: 'maven-publish'

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                    artifact source: signJar.outputs.files.singleFile, extension: 'jar.asc'
                    artifact source: signJavadocJar.outputs.files.singleFile, classifier: 'javadoc', extension: 'jar.asc'
                    artifact source: signSourcesJar.outputs.files.singleFile, classifier: 'sources', extension: 'jar.asc'
                }
            }
            repositories {
                maven {
                    url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials {
                        username = ossrhUser
                        password = ossrhPassword
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}

/**
 * Wrapper definition
 */

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
